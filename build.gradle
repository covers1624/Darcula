plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

group 'net.covers1624.bulenkov'
archivesBaseName = 'Darcula'
version '1.0'

targetCompatibility = "1.8"
sourceCompatibility = "1.8"

version = "$version." + (System.getenv("BUILD_NUMBER") ?: "1")
println "Starting build of ${archivesBaseName}, Version: ${version}"

configurations {
    shadow
    implementation.extendsFrom shadow
}

repositories {
    mavenCentral()
}

dependencies {
    shadow fileTree("lib/")

    compileOnly 'org.jetbrains:annotations:16.0.2'
}

jar {
    from file("LICENSE.txt")
}

task srcJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
    from file("LICENSE.txt")
}

shadowJar {
    build.dependsOn it
    configurations = [project.configurations.shadow]
    classifier = ""

    from file("LICENSE.txt")
}
jar.enabled = false

publishing {
    repositories {
        if (System.getenv('MAVEN_PASS')) {
            maven {
                url "https://nexus.covers1624.net/repository/maven-releases/"
                credentials {
                    username 'covers1624'
                    password System.getenv('MAVEN_PASS')
                }
            }
        }
    }
    publications {
        publication(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            artifact shadowJar
            artifact srcJar

            pom {
                name = archivesBaseName
                description = archivesBaseName
                //The publish plugin doesnt like GString's here apparently..
                url = "https://github.com/covers1624/${archivesBaseName}".toString()
                scm {
                    url = "https://github.com/covers1624/${archivesBaseName}".toString()
                    connection = "scm:git:git://github.com/covers1624/${archivesBaseName}.git".toString()
                    connection = "scm:git:git@github.com:covers1624/${archivesBaseName}.git".toString()
                }

                licenses {
                    license {
                        name = "Apache 2.0 License"
                        url = "https://raw.githubusercontent.com/covers1624/${archivesBaseName}/master/LICENSE.txt".toString()
                        distribution = 'repo'
                    }
                }
            }
        }
    }
}
